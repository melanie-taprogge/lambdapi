(** Implementation of the rewrite tactic. *)

open Lplib
open Common open Pos open Error open Debug
open Core open Term open Print
open Goal

(** Logging function for the rewrite tactic. *)
let log = Logger.make 'r' "rtac" "rewrite tactic"
let log = log.pp

(** Equality configuration. *)
type eq_config =
  { symb_P     : sym (** Encoding of propositions.        *)
  ; symb_T     : sym (** Encoding of types.               *)
  ; symb_eq    : sym (** Equality proposition.            *)
  ; symb_eqind : sym (** Induction principle on equality. *)
  ; symb_refl  : sym (** Reflexivity of equality.         *) }

(** Additional configuration needed only when rewrite has to lift an equality
    over a binder. *)
type binder_rewrite_config =
  { symb_arr    : sym (** HOL non-dependent function type constructor. *)
  ; symb_funext : sym (** Functional extensionality principle.        *) }

(** [get_eq_config ss pos] returns the current configuration for
    equality, used by tactics such as “rewrite” or “reflexivity”. *)
let get_eq_config : Sig_state.t -> popt -> eq_config = fun ss pos ->
  let builtin = Builtin.get ss pos [] in
  { symb_P     = builtin "Prf"
  ; symb_T     = builtin "El"
  ; symb_eq    = builtin "eq"
  ; symb_eqind = builtin "eqind"
  ; symb_refl  = builtin "refl" }

(** [get_binder_rewrite_config ss pos] returns the configuration needed for
    binder-dependent rewrite occurrences. *)
let get_binder_rewrite_config :
    Sig_state.t -> popt -> binder_rewrite_config = fun ss pos ->
  let builtin = Builtin.get ss pos [] in
  { symb_arr    = builtin "arr"
  ; symb_funext = builtin "funExt" }

(** [get_eq_data pos cfg a] returns [((a,l,r),[v1;..;vn])] if [a ≡ Π v1:A1,
   .., Π vn:An, P (eq a l r)] and fails otherwise. *)
let get_eq_data :
  eq_config -> popt -> term -> (term * term * term) * var list = fun cfg ->
  let exception Not_eq of term in
  let get_eq_args u =
    if Logger.log_enabled () then log "get_eq_args %a" term u;
    match get_args u with
    | eq, [a;l;r] when is_symb cfg.symb_eq eq -> a, l, r
    | _ -> raise (Not_eq u)
  in
  let exception Not_P of term in
  let return vs r = r, List.rev vs in
  let rec get_eq vs t notin_whnf =
    if Logger.log_enabled () then log "get_eq %a" term t;
    match get_args t with
    | Prod(_,t), _ ->
        (* We prefix variable names by "$" to distinguish them from the
           variables occurring in other assumptions or in the goal, and
           because we will try to match them with some subterm of the goal. *)
        let v,t = unbind ~name:("$"^binder_name t) t in get_eq (v::vs) t true
    | p, [u] when is_symb cfg.symb_P p ->
      begin
        let u = Eval.whnf ~tags:[NoRw;NoExpand] [] u in
        try return vs (get_eq_args u)
        with Not_eq _ ->
          (try return vs (get_eq_args (Eval.whnf [] u))
           with Not_eq _ when notin_whnf -> get_eq vs (Eval.whnf [] t) false)
      end
    | _ ->
      if notin_whnf then get_eq vs (Eval.whnf [] t) false
      else raise (Not_P t)
  in
  fun pos t ->
    if Logger.log_enabled () then log "get_eq_data %a" term t;
    try get_eq [] t true with
    | Not_P u ->
      fatal pos "Expected %a _ but found %a." sym cfg.symb_P term u
    | Not_eq u ->
      fatal pos "Expected %a _ _ but found %a." sym cfg.symb_eq term u

(** Type of a term with the free variables that need to be substituted. It is
   usually used to store the LHS of a proof of equality, together with the
   variables that were quantified over. *)
type to_subst = var array * term

(** [matches p t] instantiates the [TRef]'s of [p] so that [p] gets equal
   to [t] and returns [true] if all [TRef]'s of [p] could be instantiated, and
   [false] otherwise. *)
let matches : term -> term -> bool =
  let exception Not_equal in
  let add_eqs xs = List.fold_left2 (fun l pi ti -> (xs,pi,ti)::l) in
  let rec eq l =
    match l with
    | [] -> ()
    | (xs,p,t)::l ->
      if Term.cmp p t = 0 then eq l else begin
      let hp, ps, kp = get_args_len p and ht, ts, kt = get_args_len t in
      if Logger.log_enabled() then
        log "%a %a \nmatches? %a %a"
          term hp (D.list term) ps term ht (D.list term) ts;
      match hp with
      | Wild -> assert false (* used in user syntax only *)
      | Patt _ -> assert false (* used in rules only *)
      | Plac _ -> assert false (* used in scoping only *)
      | Appl _ -> assert false (* not possible after get_args_len *)
      | Type -> assert false (* not possible because of typing *)
      | Kind -> assert false (* not possible because of typing *)
      | Bvar _ -> assert false (* used in reduction only *)
      | TRef r ->
        if kp > kt then raise Not_equal;
        let ts1, ts2 = List.cut ts (kt-kp) in
        let u = add_args ht ts1 in
        if List.exists (fun x -> occur x u) xs then raise Not_equal;
        if Logger.log_enabled() then log (Color.red "<TRef> ≔ %a") term u;
        Timed.(r := Some u);
        eq (add_eqs xs l ps ts2)
      | Meta _ -> eq l (* We assume that metas can always be instantiated by
                          the corresponding RHS although this might not be the
                          case, in which case tac_refine or solve will later
                          fail. This way, we may chose the wrong subterm. *)
      | Prod _
      | Abst _
      | LLet _
      | Symb _
      | Vari _ ->
        if kp <> kt then raise Not_equal;
        match hp, ht with
        | Vari x, Vari y when eq_vars x y -> eq (add_eqs xs l ps ts)
        | Symb f, Symb g when f == g -> eq (add_eqs xs l ps ts)
        | Abst(a,b), Abst(a',b')
        | Prod(a,b), Prod(a',b') ->
            let x,b,b' = unbind2 b b' in
            eq ((xs,a,a')::(x::xs,b,b')::add_eqs xs l ps ts)
        | LLet(a,c,b), LLet(a',c',b') ->
            let x,b,b' = unbind2 b b' in
            eq ((xs,a,a')::(xs,c,c')::(x::xs,b,b')
                ::add_eqs xs l ps ts)
        | _ ->
          if Logger.log_enabled() then log "distinct heads";
          raise Not_equal
      end
  in
  fun p t ->
  let r = try eq [[],p,t]; true with Not_equal -> false in
  if Logger.log_enabled() then log "matches result: %b" r; r

let no_match ?(subterm=false) pos p t =
  if subterm then fatal pos "No subterm of [%a] matches [%a]." term t term p
  else fatal pos "[%a] doesn't match [%a]." term t term p

(** [matching_subs (xs,p) t] attempts to match the pattern [p] containing the
   variables [xs]) with the term [t]. If successful, it returns [Some ts]
   where [ts] is an array of terms such that substituting [xs] by the
   corresponding elements of [ts] in [p] yields [t].

WARNING: Some elements of [ts] may be uninstantiated TRef's. It will happen if
not all [vars] are in [LibTerm.free_vars p], for instance when some [vars] are
type variables not occurring in [p], for instance when trying to apply an
equation of the form [x = ...] with [x] of polymorphic type. This could be
improved by generating p_terms instead of terms. Indeed, in this case, we
could replace uninstantiated TRef's by underscores. *)
let matching_subs : to_subst -> term -> term array option = fun (xs,p) t ->
  (* We replace [xs] by fresh [TRef]'s. *)
  let ts = Array.map (fun _ -> mk_TRef(Timed.ref None)) xs in
  let p = msubst (bind_mvar xs p) ts in
  if matches p t then Some ts else None

(** [check_subs vars ts] check that no element of [ts] is an uninstantiated
    TRef. *)
let check_subs pos vars ts =
  let f i ti =
    match unfold ti with
    | TRef _ ->
        fatal pos "Don't know how to instantiate the argument \"%a\" \
                   of the equation." var vars.(i)
    | _ -> ()
  in
  Array.iteri f ts

let matching_subs_check_TRef pos ((vars,p) as xsp) t =
  match matching_subs xsp t with
  | Some ts -> check_subs pos vars ts; ts
  | None -> no_match pos p t

(** [find_subst (xs,p) t] tries to find the first instance of a subterm of [t]
   matching [p]. If successful, the function returns the array of terms by
   which [xs] must be substituted. *)
let find_subst : to_subst -> term -> term array option = fun xsp t ->
  let time = Timed.Time.save () in
  let rec find : term -> term array option = fun t ->
    if Logger.log_enabled() then
      log "find_subst %a ≡ %a" term (snd xsp) term t;
    match matching_subs xsp t with
    | None ->
        begin
          Timed.Time.restore time;
          match unfold t with
            | Appl(a,b) -> find2 a b
            | Abst(a,b) | Prod(a,b) -> let _,b = unbind b in find2 a b
            | LLet(a,c,b) -> let _,b = unbind b in find3 a c b
            | _ -> None
        end
    | sub -> sub
  and find2 a b =
    match find a with
    | None -> Timed.Time.restore time; find b
    | sub -> sub
  and find3 a b c =
    match find a with
    | None -> Timed.Time.restore time; find2 b c
    | sub -> sub
  in find t

let find_subst pos (vars,p) t =
  match find_subst (vars,p) t with
  | None -> no_match ~subterm:true pos p t
  | Some ts -> check_subs pos vars ts; ts

(** Binder frame enclosing a subterm found by [find_subst_under_binders]. *)
type binder_frame =
  { binder_var : var
  ; binder_typ : term
  ; binder_path : int list }

(** Result of a successful binder-aware rewrite occurrence search. *)
type subst_under_binders =
  { subst : term array
  ; redex : term
  ; binders : binder_frame list }

(** [find_subst_under_binders_from binders path (xs,p) t] is like
    [find_subst (xs,p) t], but also records the binders enclosing the matched
    occurrence. [binders] and [path] are the already-known enclosing binder
    context of [t]. The binder list is ordered from innermost to outermost
    binder. *)
let find_subst_under_binders_from :
    binder_frame list -> int list -> to_subst -> term ->
    subst_under_binders option = fun initial_binders initial_path xsp t ->
  let time = Timed.Time.save () in
  let rec find :
      binder_frame list -> int list -> term -> subst_under_binders option =
    fun binders path t ->
    if Logger.log_enabled() then
      log "find_subst_under_binders %a ≡ %a" term (snd xsp) term t;
    match matching_subs xsp t with
    | None ->
        begin
          Timed.Time.restore time;
          match unfold t with
          | Appl(a,b) -> find2 binders path a b
          | Abst(a,b) | Prod(a,b) -> find_binder binders path a b
          | LLet(a,c,b) -> find3 binders path a c b
          | _ -> None
        end
    | Some subst -> Some { subst; redex = t; binders }
  and find2 binders path a b =
    match find binders (0::path) a with
    | None -> Timed.Time.restore time; find binders (1::path) b
    | r -> r
  and find3 binders path a c b =
    match find binders (0::path) a with
    | Some _ as r -> r
    | None ->
        Timed.Time.restore time;
        match find binders (1::path) c with
        | None ->
            Timed.Time.restore time;
            let x,b = unbind b in
            let frame =
              { binder_var = x; binder_typ = a; binder_path = 2::path }
            in
            find (frame::binders) (2::path) b
        | r -> r
  and find_binder :
      binder_frame list -> int list -> term -> binder ->
      subst_under_binders option =
    fun binders path a b ->
    match find binders (0::path) a with
    | None ->
        Timed.Time.restore time;
        let x,b = unbind b in
        let frame =
          { binder_var = x; binder_typ = a; binder_path = 1::path }
        in
        find (frame::binders) (1::path) b
    | r -> r
  in find initial_binders initial_path t

(** [find_subst_under_binders (xs,p) t] is like [find_subst (xs,p) t], but
    also records the binders enclosing the matched occurrence. The binder list
    is ordered from innermost to outermost binder. *)
let find_subst_under_binders :
    to_subst -> term -> subst_under_binders option =
  find_subst_under_binders_from [] []

(** [find_subst_under_binders pos (vars,p) t] returns the first occurrence
    search result for [p] in [t], or fails if no subterm matches. It also
    checks that all variables in [vars] have been instantiated. *)
let find_subst_under_binders :
    popt -> to_subst -> term -> subst_under_binders = fun pos (vars,p) t ->
  match find_subst_under_binders (vars,p) t with
  | None -> no_match ~subterm:true pos p t
  | Some r -> check_subs pos vars r.subst; r

(** [find_subst_in_region_under_binders pos binders (vars,p) t] searches for
    [p] in an already-selected region [t], preserving the binder context
    enclosing the region. *)
let find_subst_in_region_under_binders :
    popt -> binder_frame list -> to_subst -> term -> subst_under_binders =
  fun pos binders (vars,p) t ->
  match find_subst_under_binders_from binders [] (vars,p) t with
  | None -> no_match ~subterm:true pos p t
  | Some r -> check_subs pos vars r.subst; r

(** [find_subterm_matching p t] tries to find a subterm of [t] that matches
   [p] by instantiating the [TRef]'s of [p].  In case of success, the function
   returns [true]. *)
let find_subterm_matching : term -> term -> bool = fun p t ->
  let time = Timed.Time.save () in
  let rec find : term -> bool = fun t ->
    matches p t ||
      begin
        Timed.Time.restore time;
        match unfold t with
        | Appl(a,b) -> find2 a b
        | Abst(a,b) | Prod(a,b) -> let _,b = unbind b in find2 a b
        | LLet(a,c,b) -> let _,b = unbind b in find3 a c b
        | _ -> false
      end
  and find2 a b =
    match find a with
    | false -> Timed.Time.restore time; find b
    | true  -> true
  and find3 a c b =
    match find a with
    | false -> Timed.Time.restore time; find2 c b
    | true -> true
  in find t

(** [replace_wild_by_tref t] substitutes every wildcard of [t] by a fresh
   [TRef]. *)
let rec replace_wild_by_tref : term -> term = fun t ->
  match unfold t with
  | Wild -> mk_TRef(Timed.ref None)
  | Appl(t,u) ->
    mk_Appl_not_canonical(replace_wild_by_tref t, replace_wild_by_tref u)
  | _ -> t

let find_subterm_matching pos p t =
  let p_refs = replace_wild_by_tref p in
  if not (find_subterm_matching p_refs t) then
    no_match ~subterm:true pos p t;
  p_refs

(** Term selected by a rewrite selector pattern, together with the binders
    enclosing the selected occurrence. *)
type selected_term_under_binders =
  { selected : term
  ; selected_binders : binder_frame list }

(** [find_subterm_matching_under_binders p t] is like
    [find_subterm_matching p t], but it also records the binders enclosing the
    selected subterm. The binder list is ordered from innermost to outermost
    binder. *)
let find_subterm_matching_under_binders :
    term -> term -> selected_term_under_binders option = fun p t ->
  let p_refs = replace_wild_by_tref p in
  let time = Timed.Time.save () in
  let rec find :
      binder_frame list -> int list -> term ->
      selected_term_under_binders option =
    fun binders path t ->
    if matches p_refs t then Some
        { selected = p_refs; selected_binders = binders }
    else
      begin
        Timed.Time.restore time;
        match unfold t with
        | Appl(a,b) -> find2 binders path a b
        | Abst(a,b) | Prod(a,b) -> find_binder binders path a b
        | LLet(a,c,b) -> find3 binders path a c b
        | _ -> None
      end
  and find2 binders path a b =
    match find binders (0::path) a with
    | None -> Timed.Time.restore time; find binders (1::path) b
    | r -> r
  and find3 binders path a c b =
    match find binders (0::path) a with
    | Some _ as r -> r
    | None ->
        Timed.Time.restore time;
        match find binders (1::path) c with
        | None ->
            Timed.Time.restore time;
            let x,b = unbind b in
            let frame =
              { binder_var = x; binder_typ = a; binder_path = 2::path }
            in
            find (frame::binders) (2::path) b
        | r -> r
  and find_binder :
      binder_frame list -> int list -> term -> binder ->
      selected_term_under_binders option =
    fun binders path a b ->
    match find binders (0::path) a with
    | None ->
        Timed.Time.restore time;
        let x,b = unbind b in
        let frame =
          { binder_var = x; binder_typ = a; binder_path = 1::path }
        in
        find (frame::binders) (1::path) b
    | r -> r
  in find [] [] t

(** [find_subterm_matching_under_binders pos p t] returns the first selected
    subterm matching [p] in [t], or fails if no subterm matches. *)
let find_subterm_matching_under_binders :
    popt -> term -> term -> selected_term_under_binders = fun pos p t ->
  match find_subterm_matching_under_binders p t with
  | None -> no_match ~subterm:true pos p t
  | Some r -> r

(** [bind_pattern p t] replaces in the term [t] every occurence of the pattern
   [p] by a fresh variable, and returns the binder on this variable. *)
let bind_pattern : term -> term -> binder =  fun p t ->
  let z = new_var "z" in
  let rec replace : term -> term = fun t ->
    if matches p t then mk_Vari z else
    match unfold t with
    | Appl(t,u) -> mk_Appl (replace t, replace u)
    | Prod(a,b) ->
        let x,b = unbind b in
        mk_Prod (replace a, bind_var x (replace b))
    | Abst(a,b) ->
        let x,b = unbind b in
        mk_Abst (replace a, bind_var x (replace b))
    | LLet(typ, def, body) ->
        let x, body = unbind body in
        mk_LLet (replace typ, replace def, bind_var x (replace body))
    | Meta(m,ts) -> mk_Meta (m, Array.map replace ts)
    | Bvar _ -> assert false
    | Wild -> assert false
    | TRef _ -> assert false
    | Patt _ -> assert false
    | Plac _ -> assert false
    | _ -> t
  in
  bind_var z (replace t)

(** [abstract_over_frame frame t] abstracts [t] over the variable recorded in
    [frame], using the frame's binder type. *)
let abstract_over_frame : binder_frame -> term -> term =
  fun frame t ->
  mk_Abst(frame.binder_typ, bind_var frame.binder_var t)

(** [abstract_over_frames frames t] abstracts [t] over all [frames]. The frame
    list must be ordered from innermost to outermost binder, and the returned
    abstractions are nested from outermost to innermost binder. *)
let abstract_over_frames : binder_frame list -> term -> term =
  fun frames t ->
  List.fold_left
    (fun t frame -> abstract_over_frame frame t)
    t frames

(** [apply_to_frames t frames] applies [t] to the variables in [frames]. The
    frame list must be ordered from innermost to outermost binder. *)
let apply_to_frames : term -> binder_frame list -> term =
  fun t frames ->
  List.fold_right
    (fun frame t -> mk_Appl(t, mk_Vari frame.binder_var))
    frames t

(** [adapt_to_frames frames frames' t] adapts [t], which may contain the
    variables bound by [frames], so that it refers to the corresponding
    variables bound by [frames']. *)
let adapt_to_frames :
    binder_frame list -> binder_frame list -> term -> term =
  fun frames frames' t ->
  apply_to_frames (abstract_over_frames frames t) frames'

(** [same_binder_paths frames frames'] tells whether [frames] and [frames']
    identify the same original enclosing binders across independent
    traversals of the goal. *)
let same_binder_paths : binder_frame list -> binder_frame list -> bool =
  List.equal (fun f f' -> f.binder_path = f'.binder_path)

(** [same_under_binders first_frames frames first t] tells whether [t] is the
    same term as [first], modulo the fresh variables generated when unbinding
    the same original enclosing binders. *)
let same_under_binders :
    binder_frame list -> binder_frame list -> term -> term -> bool =
  fun first_frames frames first t ->
  same_binder_paths first_frames frames
  && Term.cmp
       (abstract_over_frames first_frames first)
       (abstract_over_frames frames t) = 0

(** [same_occurrence_class first_subst first_frames frames subst] tells
    whether [subst], found under [frames], belongs to the occurrence class
    determined by [first_subst]. Occurrence arguments are compared with
    [same_under_binders], so selected occurrences must refer to the same
    original binders as the first occurrence. *)
let same_occurrence_class :
    term array -> binder_frame list -> binder_frame list -> term array ->
    bool =
  fun first_subst first_frames frames subst ->
  Array.length first_subst = Array.length subst
  && Array.for_all2 (same_under_binders first_frames frames)
    first_subst subst

(** [matching_subs_for_replacement xsp t f] tries to match [xsp] against [t]
    and calls [f] with the produced substitution while the matching references
    are still alive. It restores them before returning. *)
let matching_subs_for_replacement :
    to_subst -> term -> (term array -> 'a option) -> 'a option =
  fun xsp t f ->
  let time = Timed.Time.save () in
  match matching_subs xsp t with
  | None -> Timed.Time.restore time; None
  | Some subst ->
      let result = f subst in
      Timed.Time.restore time;
      result

(** [replace_under_binders_from binders path f goal] traverses [goal] while
    recording enclosing binder frames. [binders] and [path] are the
    already-known enclosing binder context of [goal]. At each subterm [t],
    [f binders t] may return a replacement. If it does, traversal does not
    descend below [t]. *)
let replace_under_binders_from :
    binder_frame list -> int list ->
    (binder_frame list -> term -> term option) -> term -> term =
  fun initial_binders initial_path f goal ->
  let rec replace : binder_frame list -> int list -> term -> term =
    fun binders path t ->
    match f binders t with
    | Some t -> t
    | None ->
        match unfold t with
        | Appl(t,u) ->
            mk_Appl (replace binders (0::path) t,
                     replace binders (1::path) u)
        | Prod(a,b) ->
            let x,b = unbind b in
            let frame =
              { binder_var = x; binder_typ = a; binder_path = 1::path }
            in
            mk_Prod (replace binders (0::path) a,
                     bind_var x (replace (frame::binders) (1::path) b))
        | Abst(a,b) ->
            let x,b = unbind b in
            let frame =
              { binder_var = x; binder_typ = a; binder_path = 1::path }
            in
            mk_Abst (replace binders (0::path) a,
                     bind_var x (replace (frame::binders) (1::path) b))
        | LLet(typ, def, body) ->
            let x, body = unbind body in
            let frame =
              { binder_var = x; binder_typ = typ; binder_path = 2::path }
            in
            mk_LLet (replace binders (0::path) typ,
                     replace binders (1::path) def,
                     bind_var x
                       (replace (frame::binders) (2::path) body))
        | Meta(m,ts) -> mk_Meta (m, Array.map (replace binders path) ts)
        | Bvar _ -> assert false
        | Wild -> assert false
        | TRef _ -> assert false
        | Patt _ -> assert false
        | Plac _ -> assert false
        | _ -> t
  in
  replace initial_binders initial_path goal

(** [replace_under_binders f goal] traverses [goal] while recording enclosing
    binder frames. At each subterm [t], [f binders t] may return a
    replacement. If it does, traversal does not descend below [t]. *)
let replace_under_binders :
    (binder_frame list -> term -> term option) -> term -> term =
  replace_under_binders_from [] []

(** [bind_pattern_under_binders_from binders z xsp first_subst relevant goal]
    is the binder-aware counterpart of [bind_pattern]. [binders] is the
    already-known enclosing binder context of [goal]. It replaces every
    occurrence matching the first occurrence's shape by [z] applied to the
    corresponding local binder variables. *)
let bind_pattern_under_binders_from :
    binder_frame list -> var -> to_subst -> term array -> binder_frame list ->
    term -> term = fun initial_binders z xsp first_subst relevant goal ->
  let replace binders t =
    let current_relevant =
      List.filter (fun frame -> occur frame.binder_var t) binders in
    if List.length current_relevant = List.length relevant then
      matching_subs_for_replacement xsp t (fun subst ->
        if same_occurrence_class first_subst relevant current_relevant subst
        then Some (apply_to_frames (mk_Vari z) current_relevant)
        else None)
    else None
  in
  replace_under_binders_from initial_binders [] replace goal

(** [bind_pattern_under_binders z xsp first_subst relevant goal] is the
    binder-aware counterpart of [bind_pattern]. It replaces every occurrence
    matching the first occurrence's shape by [z] applied to the corresponding
    local binder variables. *)
let bind_pattern_under_binders :
    var -> to_subst -> term array -> binder_frame list -> term -> term =
  bind_pattern_under_binders_from []

(** [replace_selected_under_binders selected_binders selected replacement
    goal]
    replaces every occurrence of [selected] in [goal] by [replacement],
    adapting references to enclosing binder variables when the same original
    binder is unbound again while traversing [goal]. *)
let replace_selected_under_binders :
    binder_frame list -> term -> term -> term -> term =
  fun selected_binders selected replacement goal ->
  let selected_binders =
    List.filter (fun frame -> occur frame.binder_var selected)
      selected_binders
  in
  let replace binders t =
    let current_binders =
      List.filter (fun frame -> occur frame.binder_var t) binders
    in
    if same_under_binders selected_binders current_binders selected t then
      Some (adapt_to_frames selected_binders current_binders replacement)
    else None
  in
  replace_under_binders replace goal

(** Data prepared for the final equality-induction rewrite step. *)
type prepared_rewrite =
  { eq_type : term
  ; lhs : term
  ; rhs : term
  ; proof : term
  ; pred_bind : binder
  ; new_term : term }

(** Equality data being lifted over one or more binders. *)
type eq_data = term * term * term * term

(** [hol_domain_of_binder cfg pos typ] returns [a] when [typ] is convertible
    to [T a], and fails otherwise. *)
let hol_domain_of_binder : eq_config -> popt -> term -> term =
  fun cfg pos typ ->
  (* User rewrite rules must not be used here:
    a type such as [T o] may reduce to the underlying host type [Prop]. *)
  match get_args (Eval.whnf ~tags:[NoRw] [] typ) with
  | t, [a] when is_symb cfg.symb_T t -> a
  | _ -> fatal pos
           "Expected a non-dependent HOL binder type of the form %a _."
           sym cfg.symb_T

(** [hol_arrow_type cfg dom cod] builds the HOL function type
    [dom ⤳ cod]. *)
let hol_arrow_type : binder_rewrite_config -> term -> term -> term =
  fun cfg dom cod ->
  add_args (mk_Symb cfg.symb_arr) [dom; cod]

(** [lift_over_frame cfg binder_cfg pos frame (a,l,r,p)] lifts the equality
    data [p : P (eq a l r)] over [frame] using [funExt]. *)
let lift_over_frame : eq_config -> binder_rewrite_config -> popt ->
    binder_frame -> eq_data -> eq_data =
  fun cfg binder_cfg pos frame (eq_type, lhs, rhs, proof) ->
  let dom = hol_domain_of_binder cfg pos frame.binder_typ in
  let cod = eq_type in
  let eq_type = hol_arrow_type binder_cfg dom cod in
  let lhs = abstract_over_frame frame lhs in
  let rhs = abstract_over_frame frame rhs in
  let proof = abstract_over_frame frame proof in
  let proof =
    add_args (mk_Symb binder_cfg.symb_funext) [dom; cod; lhs; rhs; proof]
  in
  eq_type, lhs, rhs, proof

(** [prepare_eq_data_under_binders ss cfg pos binders redex data] lifts
    [data] over every enclosing binder on which [redex] depends. It returns
    the lifted equality data and the relevant binders. *)
let prepare_eq_data_under_binders :
    Sig_state.t -> eq_config -> popt -> binder_frame list -> term ->
    eq_data -> eq_data * binder_frame list =
  fun ss cfg pos binders redex (eq_type, lhs, rhs, proof) ->
  let relevant =
    List.filter (fun frame -> occur frame.binder_var redex) binders
  in
  match relevant with
  | [] -> (eq_type, lhs, rhs, proof), relevant
  | _ ->
      let binder_cfg = get_binder_rewrite_config ss pos in
      let data =
        List.fold_left
          (fun acc frame -> lift_over_frame cfg binder_cfg pos frame acc)
          (eq_type, lhs, rhs, proof) relevant
      in
      data, relevant

(** [prepare_rewrite_under_binders_from binders ss cfg pos xsp goal eq_data
    found] prepares the equality data and rewrite context for an ordinary
    rewrite occurrence. [binders] is the already-known enclosing binder
    context of [goal]. If the redex is independent from its enclosing binders,
    the old direct rewrite data is returned. Otherwise, the equality is lifted
    over the relevant binders and the context abstracts the resulting
    function-level occurrence. *)
let prepare_rewrite_under_binders_from :
    binder_frame list -> Sig_state.t -> eq_config -> popt -> to_subst ->
    term -> eq_data -> subst_under_binders -> prepared_rewrite =
  fun initial_binders ss cfg pos xsp goal (eq_type, lhs, rhs, proof) found ->
  let (eq_type, lhs, rhs, proof), relevant =
    prepare_eq_data_under_binders ss cfg pos found.binders found.redex
      (eq_type, lhs, rhs, proof)
  in
  match relevant with
  | [] ->
      let pred_bind = bind_pattern lhs goal in
      { eq_type; lhs; rhs; proof; pred_bind; new_term = subst pred_bind rhs }
  | _ ->
      let z = new_var "z" in
      let pred =
        bind_pattern_under_binders_from
          initial_binders z xsp found.subst relevant goal
      in
      let pred_bind = bind_var z pred in
      { eq_type; lhs; rhs; proof; pred_bind; new_term = subst pred_bind rhs }

(** [prepare_rewrite_under_binders ss cfg pos xsp goal eq_data found] is
    [prepare_rewrite_under_binders_from] with no initial enclosing binders. *)
let prepare_rewrite_under_binders :
    Sig_state.t -> eq_config -> popt -> to_subst -> term -> eq_data ->
    subst_under_binders -> prepared_rewrite =
  prepare_rewrite_under_binders_from []

(** [swap cfg a r l t] returns a term of type [P (eq a l r)] from a term [t]
   of type [P (eq a r l)]. *)
let swap : eq_config -> term -> term -> term -> term -> term =
  fun cfg a r l t ->
  (* We build the predicate “λx:T a, eq a l x”. *)
  let pred =
    let x = new_var "x" in
    let pred = add_args (mk_Symb cfg.symb_eq) [a; l; mk_Vari x] in
    mk_Abst(mk_Appl(mk_Symb cfg.symb_T, a), bind_var x pred)
  in
  (* We build the proof term. *)
  let refl_a_l = add_args (mk_Symb cfg.symb_refl) [a; l] in
  add_args (mk_Symb cfg.symb_eqind) [a; r; l; t; pred; refl_a_l]

(** [rewrite ss p pos gt l2r pat t] generates a term for the refine tactic
   representing the application of the rewrite tactic to the goal type
   [gt]. Every occurrence of the first instance of the left-hand side is
   replaced by the right-hand side of the obtained proof (or the reverse if
   l2r is false). [pat] is an optional SSReflect pattern. [t] is the
   equational lemma that is appied. It handles the full set of SSReflect
   patterns. *)
let rewrite : Sig_state.t -> problem -> popt -> goal_typ -> bool ->
              (term, binder) Parsing.Syntax.rwpatt option -> term -> term =
  fun ss p pos {goal_hyps=g_env; goal_type=g_type; _} l2r pat t ->

  (* Obtain the required symbols from the current signature. *)
  let cfg = get_eq_config ss pos in

  (* Extract the term from the goal type (get “u” from “P u”). *)
  let g_term =
    match get_args g_type with
    | t, [u] when is_symb cfg.symb_P t -> u
    | _ -> fatal pos "Goal not of the form (%a _)." sym cfg.symb_P
  in

  (* Infer the type of [t] (the argument given to the tactic). *)
  let g_ctxt = Env.to_ctxt g_env in
  let (t, t_type) = Query.infer pos p g_ctxt t in

  (* Check that [t_type ≡ Π x1:a1, ..., Π xn:an, P (eq a l r)]. *)
  let (a, l, r), vars = get_eq_data cfg pos t_type in
  let vars = Array.of_list vars in

  (* Apply [t] to the variables of [vars] to get a witness of the equality. *)
  let t = Array.fold_left (fun t x -> mk_Appl(t, mk_Vari x)) t vars in

  (* Reverse the members of the equation if l2r is false. *)
  let (t, l, r) = if l2r then (t, l, r) else (swap cfg a l r t, r, l) in

  (* Bind the variables in this new witness. *)
  let bound = let bind = bind_mvar vars in bind t, bind l, bind r in
  let msubst3 (b1, b2, b3) ts = msubst b1 ts, msubst b2 ts, msubst b3 ts in

  (* Obtain the different components depending on the pattern. *)
  let (a, pred_bind, new_term, t, l, r) =
    match pat with
    (* Simple rewrite, no pattern. *)
    | None ->
        (* Build a substitution from the first instance of [l] in the goal. *)
        let lhs_pattern = l in
        let found = find_subst_under_binders pos (vars, l) g_term in
        (* Build the required data from that substitution. *)
        let (t, l, r) = msubst3 bound found.subst in
        let prepared = prepare_rewrite_under_binders
            ss cfg pos (vars, lhs_pattern) g_term (a, l, r, t) found
        in
        ( prepared.eq_type, prepared.pred_bind, prepared.new_term
        , prepared.proof, prepared.lhs, prepared.rhs )

    (* Basic patterns. *)
    | Some(Rw_Term(p)) ->
        (* Find a subterm [match_p] of the goal that matches [p]. *)
        let match_p = find_subterm_matching_under_binders pos p g_term in
        let lhs_pattern = l in
        (* Build a substitution by matching [match_p] with the LHS [l]. *)
        let sigma = matching_subs_check_TRef pos (vars,l) match_p.selected in
        (* Build the data from the substitution. *)
        let (t, l, r) = msubst3 bound sigma in
        let found =
          { subst = sigma; redex = match_p.selected
          ; binders = match_p.selected_binders }
        in
        let prepared = prepare_rewrite_under_binders
            ss cfg pos (vars, lhs_pattern) g_term (a, l, r, t) found
        in
        ( prepared.eq_type, prepared.pred_bind, prepared.new_term
        , prepared.proof, prepared.lhs, prepared.rhs )

    (* Nested patterns. *)
    | Some(Rw_InTerm(p)) ->
        (* Find the selected region, then search for the rewrite redex inside
           it without dropping the binders enclosing the region. *)
        let selected = find_subterm_matching_under_binders pos p g_term in
        let lhs_pattern = l in
        let found =
          find_subst_in_region_under_binders
            pos selected.selected_binders (vars,l) selected.selected
        in
        let (t, l, r) = msubst3 bound found.subst in
        let prepared =
          prepare_rewrite_under_binders_from
            selected.selected_binders ss cfg pos (vars, lhs_pattern)
            selected.selected (a, l, r, t) found
        in
        let region_var, region_pred = unbind prepared.pred_bind in
        let new_term =
          replace_selected_under_binders
            selected.selected_binders selected.selected
            prepared.new_term g_term
        in
        let pred =
          replace_selected_under_binders
            selected.selected_binders selected.selected
            region_pred g_term
        in
        ( prepared.eq_type, bind_var region_var pred, new_term
        , prepared.proof, prepared.lhs, prepared.rhs )

    | Some(Rw_IdInTerm(p)) ->
        (* The code here works as follows: *)
        (* 1 - Try to match [p] with some subterm of the goal. *)
        (* 2 - If we succeed we do two things, we first replace [id] with its
               value, [id_val], the value matched to get [pat_l] and  try to
               match [id_val] with the LHS of the lemma. *)
        (* 3 - If we succeed we create the "RHS" of the pattern, which is [p]
               with [sigma r] in place of [id]. *)
        (* 4 - We then construct the following binders:
               a - [pred_bind_l] : A binder with a new variable replacing each
                   occurrence of [pat_l] in g_term.
               b - [pred_bind] : A binder with a new variable only replacing
                   the subterms where a rewrite happens. *)
        (* 5 - The new goal [new_term] is constructed by substituting [r_pat]
               in [pred_bind_l]. *)
        let (id,p) = unbind p in
        let p_refs = replace_wild_by_tref p in
        let selected = find_subst_under_binders pos ([|id|],p_refs) g_term in
        let id_val = selected.subst.(0) in
        let pat = bind_var id p_refs in
        (* The LHS of the pattern, i.e. the pattern with id replaced by *)
        (* id_val. *)
        let pat_l = subst pat id_val in

        (* This must match with the LHS of the equality proof we use. *)
        let sigma = matching_subs_check_TRef pos (vars,l) id_val in
        (* Build t, l, using the substitution we found. Note that r  *)
        (* corresponds to the value we get by applying rewrite to *)
        (* id val. *)
        let (t,l,r) = msubst3 bound sigma in
        let (a,l,r,t), relevant =
          prepare_eq_data_under_binders ss cfg pos selected.binders id_val
            (a, l, r, t)
        in

        (* The RHS of the pattern, i.e. the pattern with id replaced *)
        (* by the result of rewriting id_val. *)
        let pat_r = subst pat (apply_to_frames r relevant) in

        (* Build the predicate, identifying all occurrences of pat_l *)
        (* substituting them, first with pat_r, for the new goal and *)
        (* then with l_x for the lambda term. *)
        let new_term =
          replace_selected_under_binders selected.binders pat_l pat_r g_term
        in

        (* [l_x] is the pattern with [id] replaced by the variable X *)
        (* that we use for building the predicate. *)
        let x = new_var "z" in
        let l_x = subst pat (apply_to_frames (mk_Vari x) relevant) in
        let pred =
          replace_selected_under_binders selected.binders pat_l l_x g_term
        in
        let pred_bind = bind_var x pred in
        (a, pred_bind, new_term, t, l, r)

    (* Combinational patterns. *)
    | Some(Rw_TermInIdInTerm(s,p)) ->
        (* This pattern combines the previous.  First, we identify the subterm
           of [g_term] that matches with [p] where [p] contains an identifier.
           Once we have the value that the identifier in [p] has been  matched
           to, we find a subterm of it that matches with [s].  Then in all the
           occurrences of the first instance of [p] in [g_term] we rewrite all
           occurrences of the first instance of [s] in the subterm of [p] that
           was matched with the identifier. *)
        let (id,p) = unbind p in
        let p_refs = replace_wild_by_tref p in
        let sigma = find_subst pos ([|id|],p_refs) g_term in
        (* Once we get the value of id, we work with that as our main term
           since this is where s will appear and will be substituted in. *)
        let id_val = sigma.(0) in
        (* [pat] is the full value of the pattern, with the wildcards now
           replaced by subterms of the goal and [id]. *)
        let pat = bind_var id p_refs in
        let pat_l = subst pat id_val in

        (* We then try to match the wildcards in [s] with subterms of
           [id_val]. *)
        let s = find_subterm_matching pos s id_val in

        (* Now we must match s, which no longer contains any TRef's
           with the LHS of the lemma. *)
        let sigma = matching_subs_check_TRef pos (vars,l) s in
        let (t,l,r) = msubst3 bound sigma in

        (* First we work in [id_val], that is, we substitute all
           the occurrences of [l] in [id_val] with [r]. *)
        let id_bind = bind_pattern l id_val in

        (* [new_id] is the value of [id_val] with [l] replaced
           by [r] and [id_x] is the value of [id_val] with the
           free variable [x]. *)
        let new_id = subst id_bind r in
        let (x, id_x) = unbind id_bind in

        (* Then we replace in pat_l all occurrences of [id]
           with [new_id]. *)
        let pat_r = subst pat new_id in

        (* To get the new goal we replace all occurrences of
          [pat_l] in [g_term] with [pat_r]. *)
        let pred_bind_l = bind_pattern pat_l g_term in

        (* [new_term] is the type of the new goal meta. *)
        let new_term = subst pred_bind_l pat_r in

        (* Finally we need to build the predicate. First we build
           the term l_x, in a few steps. We substitute all the
           rewrites in new_id with x and we repeat some steps. *)
        let l_x = subst pat id_x in

        (* The last step to build the predicate is to substitute
           [l_x] everywhere we find [pat_l] and bind that x. *)
        let pred = subst pred_bind_l l_x in
        (a, bind_var x pred, new_term, t, l, r)

    | Some(Rw_TermAsIdInTerm(s,p)) ->
        (* This pattern is essentially a let clause.  We first match the value
           of [pat] with some subterm of the goal, and then rewrite in each of
           the occurences of [id]. *)
        let (id,pat) = unbind p in
        let s = replace_wild_by_tref s in
        let p_s = subst p s in
        (* Try to match p[s/id] with a subterm of the goal. *)
        let selected = find_subterm_matching_under_binders pos p_s g_term in
        let pat_refs = replace_wild_by_tref pat in
        (* Here we have already asserted tat an instance of p[s/id] exists
           so we know that this will match something. The step is repeated
           in order to get the value of [id]. *)
        let sub =
          matching_subs_check_TRef pos ([|id|],pat_refs) selected.selected
        in
        let id_val = sub.(0) in
        let pat = bind_var id pat_refs in
        let pat_l = subst pat id_val in
        (* This part of the term-building is similar to the previous
           case, as we are essentially rebuilding a term, with some
           subterms that are replaced by new ones. *)
        let sigma = matching_subs_check_TRef pos (vars,l) id_val in
        let (t,l,r) = msubst3 bound sigma in
        let (a,l,r,t), relevant =
          prepare_eq_data_under_binders ss cfg pos selected.selected_binders
            id_val (a, l, r, t)
        in

        (* Now to do some term building. *)
        let pat_r = subst pat (apply_to_frames r relevant) in
        let new_term =
          replace_selected_under_binders
            selected.selected_binders pat_l pat_r g_term
        in
        let x = new_var "z" in
        let p_x = subst pat (apply_to_frames (mk_Vari x) relevant) in
        let pred =
          replace_selected_under_binders
            selected.selected_binders pat_l p_x g_term
        in
        let pred_bind = bind_var x pred in
        (a, pred_bind, new_term, t, l, r)

    | Some(Rw_InIdInTerm(q)) ->
        (* This is very similar to the [Rw_IdInTerm] case. Instead of matching
           [id_val] with [l],  we try to match a subterm of [id_val] with [l],
           and then we rewrite this subterm. As a consequence,  we just change
           the way we construct a [pat_r]. *)
        let (id,q) = unbind q in
        let q_refs = replace_wild_by_tref q in
        let selected = find_subst_under_binders pos ([|id|],q_refs) g_term in
        let id_val = selected.subst.(0) in
        let pat = bind_var id q_refs in
        let pat_l = subst pat id_val in
        let lhs_pattern = l in
        let found =
          find_subst_in_region_under_binders
            pos selected.binders (vars,l) id_val
        in
        let (t,l,r) = msubst3 bound found.subst in
        let prepared =
          prepare_rewrite_under_binders_from
            selected.binders ss cfg pos (vars, lhs_pattern) id_val
            (a, l, r, t) found
        in
        let region_var, region_pred = unbind prepared.pred_bind in

        (* The new RHS of the pattern is obtained by rewriting in [id_val]. *)
        let r_val = subst pat prepared.new_term in
        let new_term =
          replace_selected_under_binders selected.binders pat_l r_val g_term
        in
        let l_x = subst pat region_pred in
        let pred =
          replace_selected_under_binders selected.binders pat_l l_x g_term
        in
        ( prepared.eq_type, bind_var region_var pred, new_term
        , prepared.proof, prepared.lhs, prepared.rhs )
  in

  (* Construct the predicate (context). *)
  let pred = mk_Abst(mk_Appl(mk_Symb cfg.symb_T, a), pred_bind) in

  (* Construct the new goal and its type. *)
  let goal_type = mk_Appl(mk_Symb cfg.symb_P, new_term) in
  let goal_term = LibMeta.make p g_ctxt goal_type in

  (* Build the final term produced by the tactic. *)
  let eqind = mk_Symb cfg.symb_eqind in
  let result = add_args eqind [a; l; r; t; pred; goal_term] in

  (* Debugging data to the log. *)
  if Logger.log_enabled () then
    begin
      log "Rewriting with:";
      log "  goal           = [%a]" term g_type;
      log "  equality proof = [%a]" term t;
      log "  equality LHS   = [%a]" term l;
      log "  equality RHS   = [%a]" term r;
      log "  pred           = [%a]" term pred;
      log "  new goal       = [%a]" term goal_type;
      log "  produced term  = [%a]" term result;
    end;

  (* Return the proof-term. *)
  result
